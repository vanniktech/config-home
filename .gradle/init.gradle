initscript {
  repositories {
     gradlePluginPortal()
  }

  dependencies {
    classpath "com.github.ben-manes:gradle-versions-plugin:0.46.0"
  }
}

rootProject {
  apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      def version = it.candidate.version
      def module = it.candidate.module
      def group = it.candidate.group
      def currentVersin = it.currentVersion

      def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { keyword -> version.toUpperCase().contains(keyword) }

      // Allow upgrading to a new alpha / beta when we're using one.
      def currentAlphaBeta = ['ALPHA', 'BETA', "RC"].any { keyword -> currentVersin.toUpperCase().contains(keyword) }

      def isJacoco = group == "org.jacoco"

      def regex = /^[0-9,.v-]+(-r)?$/
      return (!stableKeyword && !(version ==~ regex) && !currentAlphaBeta) || (version == "2.71828") || isJacoco
    }
  }
}